apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def autoIncrementVersion() {
    def versionPropsFile = rootProject.file('version.properties')
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    def value = 0
    def runTasks = gradle.startParameter.taskNames
    for(String task : runTasks) {
        println task
        if (task.contains("assembleRuntimeRelease")) {
            value = 1
        }
    }

    def versionMinor = versionProps['VERSION_MINOR'].toInteger()
    def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
    if (versionPatch == 100) {
        versionMinor = versionMinor + 1
        versionPatch = 0
    }

    versionProps['VERSION_CODE'] = (versionProps['VERSION_CODE'].toInteger() + value).toString()
//    versionProps['VERSION_MAJOR'] = versionProps['VERSION_MAJOR']
    versionProps['VERSION_MINOR'] = versionMinor.toString()
    versionProps['VERSION_PATCH'] = versionPatch.toString()

    versionProps.store(versionPropsFile.newWriter(), null)
    return versionProps
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    Properties versionProps = autoIncrementVersion()

    defaultConfig {
        applicationId "com.terry.jetpackmvvm.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode versionProps['VERSION_CODE'].toInteger()
        versionName "${versionProps['VERSION_MAJOR']}.${versionProps['VERSION_MINOR']}.${versionProps['VERSION_PATCH']}"
        // Rename apk/aab file name
        setProperty("archivesBaseName", "app" + "-" + versionName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            if (keystoreProperties['storeFile']) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "default"

    productFlavors {
        nativalTest {
            dimension "default"
            applicationIdSuffix = ".debug"
        }
        runtime {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'com.google.android.material:material:1.4.0'
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "androidx.activity:activity-ktx:1.3.1"
    implementation "androidx.fragment:fragment-ktx:1.3.1"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    // RxBinding
    implementation 'com.jakewharton.rxbinding4:rxbinding:4.0.0'
    implementation 'com.uber.autodispose2:autodispose:2.1.0'
    implementation 'com.uber.autodispose2:autodispose-lifecycle:2.1.0'
    implementation 'com.uber.autodispose2:autodispose-android:2.1.0'
    implementation 'com.uber.autodispose2:autodispose-androidx-lifecycle:2.1.0'

    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'

    implementation 'com.github.CarGuo.GSYVideoPlayer:GSYVideoPlayer:v8.1.7-release-jitpack'

    implementation project(':textbannerlibrary')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}